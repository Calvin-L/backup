plugins {
    id "java"
    id "application"
    id "org.checkerframework" version "0.6.34"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

version = "1.0"

checkerFramework {
  checkers = [
    "org.checkerframework.checker.nullness.NullnessChecker",
    "org.checkerframework.checker.interning.InterningChecker",
    "org.checkerframework.checker.regex.RegexChecker",
    "org.checkerframework.checker.resourceleak.ResourceLeakChecker",
  ]
  extraJavacArgs = [
    "-Astubs=${projectDir}/src/stubs/",
    "-AresourceLeakIgnoredExceptions=java.lang.Error,java.lang.NullPointerException",
  ]
}

////////////////////////// Application setup
application {
    mainClass = "cal.bkup.Main"
}

////////////////////////// External deps
repositories {
    mavenCentral()
}

def aws_sdk_version = "2.25.23"
def checker_framework_version = "3.42.0"

dependencies {
    implementation project(":aes-crypt")
    implementation project(":crash-safe-io")
    implementation project(":aws-ports")
    implementation "software.amazon.awssdk:glacier:${aws_sdk_version}"
    implementation "software.amazon.awssdk:s3:${aws_sdk_version}"
    implementation "software.amazon.awssdk:dynamodb:${aws_sdk_version}"
    implementation "org.xerial:sqlite-jdbc:3.45.2.0"
    implementation "org.tukaani:xz:1.9"
    implementation "commons-cli:commons-cli:1.6.0"
    implementation "com.fasterxml.jackson.core:jackson-databind:2.17.0"
    implementation "org.apache.commons:commons-math3:3.6.1"
    implementation "org.checkerframework:checker-qual:${checker_framework_version}"
    checkerFramework "org.checkerframework:checker:${checker_framework_version}"
    implementation "com.google.guava:guava:33.1.0-jre"
    testImplementation "org.testng:testng:7.9.0"

    // The AWS SDK issues a warning if the JAXB classes are not available.
    // There is a fallback implementation so functionality is not affected,
    // but the warning is annoying enough to be worth silencing.
    runtimeOnly "jakarta.xml.bind:jakarta.xml.bind-api:4.0.2"
}

////////////////////////// Testing
test { useTestNG() }
